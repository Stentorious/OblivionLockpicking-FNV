; Oblivion Lockpicking - Stentorious

; On game load
SetUIFloatAlt "HUDMainMenu\OblivionLockpicking\visible" 0
EK 17					; W
EK 18					; E
EK 30					; A
EK 31					; S
EK 32					; D
EK 33					; F
SetStickDisabled 1 0	; Right Stick

; On Game Restart
if eval Goo1.AuxVarGetFlt "*OblLock_Init"
	return
endif
Goo1.AuxVarSetFlt "*OblLock_Init" 1

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Oblivion Lockpicking missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 508
	MessageBoxEx "Oblivion Lockpicking missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 145
	MessageBoxEx "Oblivion Lockpicking missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 905
	MessageBoxEx "Oblivion Lockpicking missing requirement!%rInstall lStewieAl's Tweaks 9.05+."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Oblivion Lockpicking missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif

; Load INI settings
Goo1.AuxVarSetFlt "*OblLock_INI" (eval GetINIFloat_Cached "General:bDoorLocks" "Stentorious\OblivionLockpicking.ini" 1) 0
Goo1.AuxVarSetFlt "*OblLock_INI" (eval GetINIFloat_Cached "General:bContainerLocks" "Stentorious\OblivionLockpicking.ini" 1) 1
Goo1.AuxVarSetFlt "*OblLock_INI" (eval GetINIFloat_Cached "Experimental:bMouseControls" "Stentorious\OblivionLockpicking.ini" 0) 2
Goo1.AuxVarSetFlt "*OblLock_INI" (Clamp (GetINIFloat_Cached "Difficulty:iBreakLockChance" "Stentorious\OblivionLockpicking.ini" 50) 0 100) 3
Goo1.AuxVarSetFlt "*OblLock_INI" (Clamp (GetINIFloat_Cached "Difficulty:iPinResetBehavior" "Stentorious\OblivionLockpicking.ini" 2) 0 2) 4
Goo1.AuxVarSetFlt "*OblLock_INI" (GetMaxOf 0 (GetINIFloat_Cached "Difficulty:fRaiseTimeMinBase" "Stentorious\OblivionLockpicking.ini" 0.05)) 5
Goo1.AuxVarSetFlt "*OblLock_INI" (GetMaxOf 0 (GetINIFloat_Cached "Difficulty:fRaiseTimeMinSkill" "Stentorious\OblivionLockpicking.ini" 0)) 6
Goo1.AuxVarSetFlt "*OblLock_INI" (GetMaxOf 0 (GetINIFloat_Cached "Difficulty:fRaiseTimeMaxBase" "Stentorious\OblivionLockpicking.ini" 0.15)) 7
Goo1.AuxVarSetFlt "*OblLock_INI" (GetMaxOf 0 (GetINIFloat_Cached "Difficulty:fRaiseTimeMaxSkill" "Stentorious\OblivionLockpicking.ini" 0.15)) 8
Goo1.AuxVarSetFlt "*OblLock_INI" (GetMaxOf 0 (GetINIFloat_Cached "Difficulty:fPeakTimeMul" "Stentorious\OblivionLockpicking.ini" 0.75)) 9
Goo1.AuxVarSetFlt "*OblLock_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:iMinigameChance" "Stentorious\OblivionLockpicking.ini" 100)) 10
CloseFileSO "Stentorious\OblivionLockpicking.ini" 0

; Cache GameSettings
Goo1.AuxVarSetFlt "*GS_fLockSkillBase" (GetGS "fLockSkillBase")

; Update tutorial messages
SetFormDescription HelpLockpicking (GetINIString_Cached "Strings:sHelpLockpicking" "Stentorious\Translate\OblivionLockpicking.ini")
SetFormDescription HelpLockpickingXBox (GetINIString_Cached "Strings:sHelpLockpickingXBox" "Stentorious\Translate\OblivionLockpicking.ini")
CloseFileSO "Stentorious\Translate\OblivionLockpicking.ini" 0

; Disable menu sounds
SetSoundTraitNumeric UILockpickingPickMovement 3 -100
SetSoundFlag UILockpickingPickTensionLPM 9 0
SetSoundSourceFile UILockpickingPickTensionLPM "sound\fx\ui\lockpicking\pickmovement\"
;SetSoundSourceFile UILockpickingUnlock "Sound\Stentorious\OblivionLockpicking\ui_lockpicking_success.wav"
;SetSoundSourceFile UILockpickingPickBreak "Sound\Stentorious\OblivionLockpicking\ui_lock_pick_break.wav"

; On LockPickMenu open
SetOnMenuOpenEventHandler (CompileScript "OblivionLockpicking\OpenLockPickMenu.gek") 1 1014

; Keyboard inputs
SetOnKeyDownEventHandler (CompileScript "OblivionLockpicking\InputHandler.gek") 1 17	; W
SetOnKeyDownEventHandler (CompileScript "OblivionLockpicking\InputHandler.gek") 1 18	; E
SetOnKeyDownEventHandler (CompileScript "OblivionLockpicking\InputHandler.gek") 1 30	; A
SetOnKeyDownEventHandler (CompileScript "OblivionLockpicking\InputHandler.gek") 1 32	; D
SetOnKeyDownEventHandler (CompileScript "OblivionLockpicking\InputHandler.gek") 1 33	; 3
SetOnKeyDownEventHandler (CompileScript "OblivionLockpicking\InputHandler.gek") 1 57	; Space

; Gamepad inputs
SetEventHandler "OnButtonDown:1" (CompileScript "OblivionLockpicking\InputHandler.gek")		; DPAD_UP
SetEventHandler "OnButtonDown:4" (CompileScript "OblivionLockpicking\InputHandler.gek")		; DPAD_LEFT
SetEventHandler "OnButtonDown:8" (CompileScript "OblivionLockpicking\InputHandler.gek")		; DPAD_RIGHT
SetEventHandler "OnButtonDown:4096" (CompileScript "OblivionLockpicking\InputHandler.gek")	; BUTTON_A
SetEventHandler "OnButtonDown:8192" (CompileScript "OblivionLockpicking\InputHandler.gek")	; BUTTON_B
SetEventHandler "OnButtonDown:16384" (CompileScript "OblivionLockpicking\InputHandler.gek")	; BUTTON_X

; Exit button
SetOnMenuClickEventHandler (begin Function {int iMenuID, int iTileID, string_var sTileName}
	Call (CompileScript "OblivionLockpicking\InputHandler.gek") 18
	sv_Destruct sTileName
end) 1 "HUDMainMenu\OblivionLockpicking\LPM_MainRect\LPM_ButtonRect\LPM_ExitButton"

; Force lock button
SetOnMenuClickEventHandler (begin Function {int iMenuID, int iTileID, string_var sTileName}
	Call (CompileScript "OblivionLockpicking\InputHandler.gek") 33
	sv_Destruct sTileName
end) 1 "HUDMainMenu\OblivionLockpicking\LPM_MainRect\LPM_ButtonRect\LPM_ForceLockButton"
